plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'de.undercouch.download' version '5.3.0'
}

archivesBaseName = "$group-$name"
description = 'TripleA Smoke Testing Application'
mainClassName = 'org.triplea.test.smoke.ClientConnect'

dependencies {
    implementation project(':game-app:domain-data')
    implementation project(':game-app:game-core')
    implementation project(':http-clients:feign-common')
    implementation project(':http-clients:lobby-client')
    testImplementation project(':game-app:game-headless')
    testImplementation project(':lib:java-extras')
    testImplementation project(':lib:test-common')
    testImplementation "org.sonatype.goodies:goodies-prefs:$sonatypeGoodiesPrefsVersion"
}

shadowJar {
    archiveClassifier.set ''
}

test {
    // AiGameTest is memory intensive due to ConcurrentBattleCalculator threads deserializing GameData concurrently.
    maxHeapSize = "2G"
}

tasks.test.dependsOn("downloadXmls", "downloadSaveGames")

task downloadXmls {
    doLast {
        mkdir "$projectDir/src/test/resources/map-xmls/"

        def urls = []
        file('map-list.txt').eachLine { path ->
            if (!path.startsWith('#')) {
                urls.add(path)
            }
        }

        // Use a single download.run invocation to download in parallel.
        download.run {
             src(urls)
             dest "$projectDir/src/test/resources/map-xmls/"
             overwrite false
        }
    }
}

task downloadSaveGames {
    doLast {
        mkdir "$projectDir/src/test/resources/save-games/"

        def urls = []
        file('save-game-list.txt').eachLine { path ->
            if (!path.startsWith('#')) {
                urls.add(path)
            }
        }

        // Use a single download.run invocation to download in parallel.
        download.run {
            src(urls)
            dest "$projectDir/src/test/resources/save-games/"
            overwrite false
        }
    }
}

clean.doFirst {
    delete "$projectDir/src/test/resources/map-xmls/"
    delete "$projectDir/src/test/resources/save-games/"
}
